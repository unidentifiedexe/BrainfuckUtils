>>>

#region 数値読み取り
+>+>+>+>+>+> +>+>+>+>+>+> //桁数
>+[-<
  ,+[-----------[----------------------
    [--------------<-[+<-]>[[-<+>]>]+<]
  ]]>
]
<<-[+<-]+>[->]
>>>
+>+>+>+>+>+> +>+>+>+>+>+> //桁数
>+[-<
  ,+[-----------[----------------------
    [--------------<-[+<-]>[[-<+>]>]+<]
  ]]>
]
<<-[+<-]+>[->]<
#endregion

#region 除算を行います。

#region Notice
//メモリ: ___{被除数}___{除数}___{商(メモリクリア状態)}___
//初期位置は、除数の末尾、終了位置は商の末尾。
//桁には余裕を持ってください。(上位2or3桁ぐらいの秋が必要)
#endregion

#region 都合により除数を被除数に加算
[
  [<] {} <<[<]<[->+<]
  >[>]>> {} [>]>+<<
  -[ [<] {} <<[<]>+[>] {} >>[>]>+<<- ]<
]
>>>[[-<<+>>]>]<<<[<]>
<<< {} 
[ 
  [->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[ 
    <+>[->+<]>----------< 
  ]]]]]]]]]]]<
]
>>[[-<<+>>]>]> {} [>]<
#endregion

#region 真の桁数を取得
[<]<<<
[<]>[-[+ [>] ]<<+>>>]<<<->>
>>>[>]<
[<]>[-[+<-> [>] ]+>]<-<
#endregion
[<]<[<]<<<

#region 被除数の擬似最上位を1にする。
[<]<++<->>>[>]<
#endregion

#region 被除数をデクリメント
#endregion

>>>>[>]>[>]<

#region 開始桁を設定
[
  [->+<]<<[<]<[<]<<<[<]
  >[-<+>]>[>]>>>[>]>[>]<
]
#endregion

#region 被除数の非計算桁を移動
<[<]<<<
[[->>+<<]<]
>>>[>]>[>]
#endregion

<[<]<[<]<<<[<]>

-[
  [>]>>>[>]>[>]>>[>]>>>>[>]+[<]<<<<[<]<<[<]<[<]<<<[<]>
  
  #region 桁ループ
  [+
    [>]>>>[>]>[>]>>[>]<
    >>>>>[>]<+[<]<<<<
    
    #region メインの引き算
    [
      [<] {<<[<]<[<]} <<[<]<[->+<]
      >+++++++++[>]>> {[>]>[>]>> } [>]>+<<
      -[ [<] {<<[<]<[<]} <<[<]>-[>]>> {[>]>[>]>>} [>]>+<<- ]<
    ]
    >>>[[-<<+>>]>]<<<[<]
    <<[<]<[<]
    <<[<]<-[->+<]<>>[>]>
    >
    <<< {} +
    [ 
      [->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[ 
        <+>[->+<]>----------< 
      ]]]]]]]]]]]<
    ]
    >
    >[[-<<+>>]>]
    #endregion
    
    <<<[<]>
    -
  ]+
  #endregion
  
  [>]>>>[>]>[>]>>[>]<
  
  #region 引きすぎるので足し算
  [
    [<] {<<[<]<[<]} <<[<]<[->+<]
    >[>]>> {[>]>[>]>> } [>]>+<<
    -[ [<] {<<[<]<[<]} <<[<]>+[>]>> {[>]>[>]>>} [>]>+<<- ]<
  ]
  >>>[[-<<+>>]>]<<<[<]
  <<[<]<[<]
  <<[<]<[[->+<]<]>>[>]>
  >
  <<< {} 
  [ 
    [->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[ 
      <+>[->+<]>----------< 
    ]]]]]]]]]]]<
  ]
  >>[[-<<+>>]>]> {} [>]<
  #endregion
  
  #region 次の桁計算のための処理
  [<]<<<[<]+>-->[>]>>>
  [
    [-<<<+>>>]<<<[<]>[>]>>
  ]
  #endregion
  <<<[<]>
]

#region 被除数の後始末
<<[[->+<]<]<[[->>+<<]<]>>>[>]
#endregion

>>>[>]>>[>]<

#region 除数の後始末
[<]<+>>[[-<+>]>]<
#endregion

>>

#region 商の後始末
>>>[-[-<<<<+>>>>]>]
<<<<<[<]>
+>
+>+>+>+>+>+>+>+>+>+>+>+> // 桁数記述
<[<]>-[>-]<
[
  
  [->>[>]>+<<[<]<]
  >+[>]<-<[<]<
]
>>[-]>[>]+>>[[-<+>]>]<<

[-[>[+++++++++>]>]+<]>-<<//被除数に除数をあらかじめ足しているのでその分を減算
#endregion


#endregion


 +[<]>[-[+[>]]<+>>] ++++++[-<-------->]<+<--> [[<]>[+>]<]<[<]>[.>]
 >++++++[-<++++++++>]<- [[<]>[->]<]<[<]<[[->+<]<]>>[>]<　//数値復帰部
 >++++++++++.[-]<
 [<]<<<[<]<<<
 
 +[<]>[-[+[>]]<+>>] ++++++[-<-------->]<+<--> [[<]>[+>]<]<[<]>[.>]
 >++++++[-<++++++++>]<- [[<]>[->]<]<[<]<[[->+<]<]>>[>]<　//数値復帰部
