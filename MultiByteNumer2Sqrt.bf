>>>
#region Multi_ReadNumber 
+>+>+>+>+> +>+> //桁数
>+[-<
  ,+[-----------[----------------------
    [--------------<-[+<-]>[[-<+>]>]+<]
  ]]>
]
<<-[+<-]+>[->]<
#endregion
>
>>>
+>+>+>+>+>+>+>
>>>
+>+>+>+>+>+>+>
<
[<]<<<[<]<<<

#region Sqrt

#region note
// 破壊Sqrt
// start: _{num*}_{ 0 }_{ 0 }_
// end  : _{res*}_{rem}_{dst}_
// res  : sqrt(num) (少数以下切り捨て)
// rem  : 余り
// dst  : 計算のゴミです必要があれば別途クリアしてください
// notice : 
//  numのメモリには余裕(2byte程度)を持ってください。
//  固定少数表示に適用する場合は、小数点の位置がずれること、
//  少数以下の桁数が偶数である必要があることに注意してください
#endregion

#region 前処理
+[<]>[-[+[>]]<<+>>>] //TODO Check
<<<->>>
<<
-[<<]
<[->>+<<]>>

#endregion

#region メインループ(開平法)

[
  [>]>>>[-]>[-]>[[-<<+>>]>]<+<+
  >>>>>[-]>[[-<+>]>]<+[<]<<<[<]>+<<<<[<]>
  -<<+>>[[>]>>>[>]<<+[<]<<<[<]>-]>
  -<<+>>[[>]>>>[>]<+[<]<<<[<]>-]
  
  >[>]>>>
  
  -
  [ +
    [>]>>>[>]<
    @
    #region 引き戻し法 
    
    #region Multi_SubFromPrev 0
    //start  ___{ a }___{ b*}___
    //resilt ___{res}___{ b*}___
    //notice a の桁が b 以上である必要があります。
    //       アンダーフロー処理済み
    [<]<<<[<]++[[-<+>]>]
    >>>[>]<
    [
      ->>+<<
      [ [<]<<<[<]<[-[>[+++++++++>]>[>]>]+<] >-< >>>[>]>+<<- ]
      <[<]<<<[<]<[->+<]>[>]>>>[>]<
    ]
    <<<[<]<[[->+<]<]>>[-]>[>]>>>>>[[-<<+>>]>]<<<
    #endregion
    
    
    
    [<]<<<[<]<<<[<]<<
    [
      -<+>
      >>>[>]>>>[>]<
      #region Multi_Decriment
      [-[>[+++++++++>]>]+<]>-<<
      #endregion
      [<]<<<[<]<<
      [
        -<+>
        >>>[>]>>>[>]<
        #region Multi_Decriment
        [-[>[+++++++++>]>]+<]>-<<
        [-[>[+++++++++>]>]+<]>-<<
        #endregion
        [<]<<<[<]<<
      ]
    ]
    <[->+<]+
    >>>>
    [>]>>>
    
    #endregion
    
    -
  ]
  +
  #region 後処理
  [>]>>>[>]<
  #region Multi_AddToPrev 0
  //start  ___{ a }___{ b*}___
  //resilt ___{res}___{ b*}___
  [
    [<]<[<]<<[->>+<<]>>[>]>[>]>+<<-
    [ [<]<[<]>+[>]>[>] >+<<- ] <
  ]
  <[<]<<[<]>
  [[-<+>]>]>>[[-<<<+>>>]>]<<<<
  [ 
    [->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[ 
      [->+<]>----------< <[+>]>[<]
    ]]]]]]]]]]]<
  ]
  >>[>]>>>>>[[-<<+>>]>]<<<
  #endregion
  
  
  
  [<]<<<
  [<]<<<[<]<<
  -[
    -<>>>>
    [>]>>>[>]>>>[>]<[<]<<<
    #region Multi_Incriment 
    >+[+++++++++<[->->+<<]+>[[-]<->]<]>>[+[-<<+>>]]>[[[-]<<+>>]>]<<<
    #endregion 
    [<]<<<[<]<<
    [
      -<+>>>>
      [>]>>>[>]>>>[>]<+ // 一個目のインクリメントは桁上がりはしない
      #region Multi_Incriment 
      >+[+++++++++<[->->+<<]+>[[-]<->]<]>>[+[-<<+>>]]>[[[-]<<+>>]>]<<<
      #endregion
      [<]<<<
      #region Multi_Incriment 
      >+[+++++++++<[->->+<<]+>[[-]<->]<]>>[+[-<<+>>]]>[[[-]<<+>>]>]<<<
      >+[+++++++++<[->->+<<]+>[[-]<->]<]>>[+[-<<+>>]]>[[[-]<<+>>]>]<<<
      #endregion 
      [<]<<<[<]<<
    ]
  ]+
  #endregion
  >>>
  
]

#endregion

#region 後処理
<<<-<
[
  [->>[>]>+<<[<]<] 
  >>[<+[>]<-]<[<]<[>]
  +<-<
  
]
>>[>]<[[->+<]<]>>[>]+[>]<
>>>>-<<<<
#endregion

#endregion


